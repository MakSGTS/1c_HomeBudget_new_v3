
&НаСервере
Процедура ОбновитьДанныеНаГлавномЭкране(Объект = Неопределено) Экспорт
	ЗаполнитьШкалуБюджетаНаСервере(Объект);
	ЗаполнитьГрафикРасходовНаСервере(Объект);
	ЗаполнитьДиаграммуДенежныеСредстваНаСервере(Объект)	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШкалуБюджетаНаСервере(Объект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	БюджетФактОбороты.СуммаОборот / БюджетПланОбороты.СуммаОборот * 100 КАК ВыполнениеБюджета,
	               |	БюджетПланОбороты.СуммаОборот КАК СуммаПлан,
	               |	БюджетФактОбороты.СуммаОборот КАК СуммаФакт
	               |ИЗ
	               |	РегистрНакопления.БюджетПлан.Обороты(&Дата1, &Дата2, , ) КАК БюджетПланОбороты,
	               |	РегистрНакопления.БюджетФакт.Обороты(&Дата1, &Дата2, , ) КАК БюджетФактОбороты";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ТекущаяДата()));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Объект.ШкалаБюджета = ВыборкаДетальныеЗаписи.ВыполнениеБюджета;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГрафикРасходовНаСервере(Объект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СтатьиРасходов.Ссылка КАК ГрупаРасходов
	               |ПОМЕСТИТЬ ВтСтатьиРасходовГруппа
	               |ИЗ
	               |	Справочник.СтатьиРасходов КАК СтатьиРасходов
	               |ГДЕ
	               |	СтатьиРасходов.ЭтоГруппа
	               |	И СтатьиРасходов.Родитель.Ссылка ЕСТЬ NULL
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПокупкиОбороты.СтатьяРасхода КАК СтатьяРасхода,
	               |	ПокупкиОбороты.СуммаОборот КАК Сумма
	               |ПОМЕСТИТЬ ВТРасходы
	               |ИЗ
	               |	РегистрНакопления.Покупки.Обороты(&Дата1, &Дата2, , ) КАК ПокупкиОбороты
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВтСтатьиРасходовГруппа.ГрупаРасходов КАК ГрупаРасходов,
	               |	ВТРасходы.Сумма КАК Сумма,
	               |	ПРЕДСТАВЛЕНИЕ(ВтСтатьиРасходовГруппа.ГрупаРасходов) КАК ГрупаРасходовПредставление
	               |ИЗ
	               |	ВТРасходы КАК ВТРасходы
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтатьиРасходовГруппа КАК ВтСтатьиРасходовГруппа
	               |		ПО (ВТРасходы.СтатьяРасхода.Родитель = ВтСтатьиРасходовГруппа.ГрупаРасходов
	               |				ИЛИ ВТРасходы.СтатьяРасхода.Родитель.Родитель = ВтСтатьиРасходовГруппа.ГрупаРасходов
	               |				ИЛИ ВТРасходы.СтатьяРасхода.Родитель.Родитель.Родитель = ВтСтатьиРасходовГруппа.ГрупаРасходов
	               |				ИЛИ ВТРасходы.СтатьяРасхода.Родитель.Родитель.Родитель.Родитель = ВтСтатьиРасходовГруппа.ГрупаРасходов
	               |				ИЛИ ВТРасходы.СтатьяРасхода.Родитель.Родитель.Родитель.Родитель.Родитель = ВтСтатьиРасходовГруппа.ГрупаРасходов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ГрупаРасходов       
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	ГрупаРасходов
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ТекущаяДата()));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ТекущаяДата()));
	
	РезультатЗапроса = Запрос.Выполнить();   
	
	Диаграмма = Объект.ДиаграммаРасходы;
	Диаграмма.ТипДиаграммы = ТипДиаграммы.КруговаяОбъемная;
	Диаграмма.ОбластьЛегенды.Прокрутка = Ложь;
	Диаграмма.ВидПодписей = ВидПодписейКДиаграмме.Процент;
	Диаграмма.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Низ;
	Диаграмма.ОбластьПостроения.ЦветФона = ЦветаСтиля.ЦветАктивности;
	Диаграмма.Окантовка = Истина;
	Диаграмма.ОбластьЛегенды.ЦветФона = ЦветаСтиля.ЦветАктивности;
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.АвтоТранспонирование = Ложь;
	
	Точка = Диаграмма.УстановитьТочку("Сумма");
	
	ВыборкаСтатьяРасхода = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСтатьяРасхода.Следующий() Цикл		
		Серия = Диаграмма.УстановитьСерию(ВыборкаСтатьяРасхода.ГрупаРасходов);
		Серия.Текст = ВыборкаСтатьяРасхода.ГрупаРасходовПредставление;
		Серия.Расшифровка = СтрШаблон("%1: %2", ВыборкаСтатьяРасхода.ГрупаРасходовПредставление, ВыборкаСтатьяРасхода.Сумма);
		Диаграмма.УстановитьЗначение(Точка, Серия, ВыборкаСтатьяРасхода.Сумма, Серия.Расшифровка);
	КонецЦикла;
	
	Диаграмма.АвтоТранспонирование = Истина;
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуДенежныеСредстваНаСервере(Объект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиДенежныхСредствОстатки.Счет КАК Счет,
	               |	ПРЕДСТАВЛЕНИЕ(ОстаткиДенежныхСредствОстатки.Счет) КАК СчетПредставление,
	               |	ОстаткиДенежныхСредствОстатки.СуммаОстаток КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.ОстаткиДенежныхСредств.Остатки(&Дата, ) КАК ОстаткиДенежныхСредствОстатки
	               |ИТОГИ
	               |	СУММА(Сумма)
	               |ПО
	               |	Счет";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Диаграмма = Объект.ДиаграммаДенежныеСредства;
	Диаграмма.ТипДиаграммы = ТипДиаграммы.Гистограмма;
	Диаграмма.ОбластьЛегенды.Прокрутка = Истина;
	Диаграмма.ВидПодписей = ВидПодписейКДиаграмме.Значение;
	Диаграмма.ОбластьПостроения.ЦветФона = ЦветаСтиля.ЦветВажного;
	Диаграмма.ОбластьЛегенды.ЦветФона = ЦветаСтиля.ЦветВажного;
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	Диаграмма.АвтоТранспонирование = Ложь;
	
	Точка = Диаграмма.УстановитьТочку("Сумма");
	
	ВыборкаСчет = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСчет.Следующий() Цикл
		Серия = Диаграмма.УстановитьСерию(ВыборкаСчет.Счет);
		Серия.Текст = ВыборкаСчет.СчетПредставление;
		Серия.Расшифровка = ВыборкаСчет.Счет;
		Диаграмма.УстановитьЗначение(Точка, Серия, ВыборкаСчет.Сумма, Серия.Расшифровка);
	КонецЦикла;
	
	Диаграмма.АвтоТранспонирование = Истина;
	Диаграмма.Обновление = Истина;

КонецПроцедуры

